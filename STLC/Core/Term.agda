module Categories.STLC.Core.Term where

open import Categories.STLC.Core.OPE

infix  4 _∈_ _⊢_
infixr 5 _⇒_

data Type : Set where
  ⋆   : Type
  _⇒_ : Type -> Type -> Type

Con = Listʳ Type

data _∈_ σ : Con -> Set where
  vz : ∀ {Γ}   -> σ ∈ Γ ▻ σ
  vs : ∀ {Γ τ} -> σ ∈ Γ     -> σ ∈ Γ ▻ τ 

data _⊢_ Γ : Type -> Set where
  var : ∀ {σ}   -> σ ∈ Γ     -> Γ ⊢ σ
  ƛ   : ∀ {σ τ} -> Γ ▻ σ ⊢ τ -> Γ ⊢ σ ⇒ τ
  _·_ : ∀ {σ τ} -> Γ ⊢ σ ⇒ τ -> Γ ⊢ σ     -> Γ ⊢ τ

renᵛ : ∀ {Γ Δ σ} -> Γ ⊆ Δ -> σ ∈ Γ -> σ ∈ Δ
renᵛ  stop     ()
renᵛ (skip ι)  v     = vs (renᵛ ι v)
renᵛ (keep ι)  vz    = vz
renᵛ (keep ι) (vs v) = vs (renᵛ ι v)

ren : ∀ {Γ Δ σ} -> Γ ⊆ Δ -> Γ ⊢ σ -> Δ ⊢ σ
ren ι (var v) = var (renᵛ ι v)
ren ι (ƛ b  ) = ƛ (ren (keep ι) b)
ren ι (f · x) = ren ι f · ren ι x
